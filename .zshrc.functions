#--------------------------------------------------
# [ ^@ ] change directory from history stack
# 
function peco-cdr () {
  local selected_dir=$(cdr -l | awk '{ print $2 }' | peco)
  if [ -n "$selected_dir" ]; then
    BUFFER="cd ${selected_dir}"
    zle accept-line
  fi
  zle clear-screen
}
zle -N peco-cdr
bindkey '^@' peco-cdr

#--------------------------------------------------
# [ ^R ] select history
# 
function peco-select-history() {
    local tac
    if which tac > /dev/null; then
        tac="tac"
    else
        tac="tail -r"
    fi
    BUFFER=$(\history -n 1 | eval $tac | awk '!a[$0]++' | peco --query "$LBUFFER")
    CURSOR=$#BUFFER
    zle clear-screen
}
zle -N peco-select-history
bindkey '^R' peco-select-history

#--------------------------------------------------
# [ ^G ] git checkout branch
# 
function peco-git-checkout() {
  local branch=$(git branch -a | peco | tr -d ' ')
  if [ -n "$branch" ]; then
    if [[ "$branch" =~ "remotes/" ]]; then
      local b=$(echo $branch | awk -F'/' '{print $3}')
      BUFFER="git checkout -b ${b} ${branch}"
    else
      BUFFER="git checkout ${branch}"
    fi
    CURSOR=$#BUFFER
  fi
  zle clear-screen
}
zle -N peco-git-checkout
bindkey '^G' peco-git-checkout
